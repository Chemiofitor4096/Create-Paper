plugins {
    id 'idea'
    id 'maven-publish'
    id "net.neoforged.moddev.legacyforge" version "2.0.74"
    id "me.modmuss50.mod-publish-plugin" version "0.8.4"
}


base {
    archivesName = "cpaper"
    group = 'com.chemiofitor.cpaper'
    version = "${mod_version}"
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

legacyForge {
    version = "$minecraft_version-$forge_version"

    validateAccessTransformers = false
    accessTransformers.from "src/main/resources/META-INF/accesstransformer.cfg"

    parchment {
        minecraftVersion = minecraft_version
        mappingsVersion = parchment_version
    }

    mods {
        cpaper {
            sourceSet sourceSets.main
        }
    }

    runs {
        // applies to all the run configs below
        configureEach {
            systemProperty 'forge.logging.markers', ''
            systemProperty 'forge.logging.console.level', 'info'
            jvmArguments = ["-XX:+IgnoreUnrecognizedVMOptions", "-XX:+AllowEnhancedClassRedefinition"]

            systemProperty 'mixin.debug.export', 'true'
            systemProperty 'mixin.debug.verbose', 'true'
        }

        client {
            client()

            gameDirectory = project.file('run')
        }

        server {
            server()

            gameDirectory = project.file('run/server')
        }

        data {
            data()
            gameDirectory = project.file('run')
            systemProperty 'forge.logging.markers', 'REGISTRIES,REGISTRYDUMP'
            systemProperty 'forge.logging.console.level', 'debug'

            programArguments.addAll(
                    "--mod", "cpaper",
                    "--all",
                    "--output",
                    file("src/generated/resources/").getAbsolutePath(),
                    "--existing",
                    file("src/main/resources").getAbsolutePath()
            )
        }

        gameTestServer {
            type = "gameTestServer"

            gameDirectory = project.file('run/gametest')
        }
    }
}

repositories {
    maven { url "https://maven.createmod.net" } // Ponder, Flywheel
    maven { url "https://maven.tterrag.com" } // Registrate
    maven { url "https://maven.blamejared.com" } // JEI
    maven { url "https://api.modrinth.com/maven" } // Farmer's Delight
}

dependencies {
    // Mixin
    implementation "io.github.llamalad7:mixinextras-forge:0.4.1"
    annotationProcessor "org.spongepowered:mixin:${mixin_version}:processor"

    // Create
    modImplementation("com.simibubi.create:create-${minecraft_version}:${create_version}-${create_version_number}:slim") {
        transitive = false
    }

    // Create Dependencies
    modImplementation "com.tterrag.registrate:Registrate:${registrate_version}"

    modImplementation "net.createmod.ponder:Ponder-Forge-${minecraft_version}:${ponder_version}"

    modRuntimeOnly "dev.engine-room.flywheel:flywheel-forge-${flywheel_minecraft_version}:${flywheel_version}"
    modCompileOnly "dev.engine-room.flywheel:flywheel-forge-api-${flywheel_minecraft_version}:${flywheel_version}"

    // Farmer's Delight
    modImplementation "maven.modrinth:farmers-delight:${farmers_delight_version}"

    // JEI
    modImplementation "mezz.jei:jei-${jei_minecraft_version}-forge:${jei_version}"
    modCompileOnly "mezz.jei:jei-${jei_minecraft_version}-common-api:${jei_version}"
    modCompileOnly "mezz.jei:jei-${jei_minecraft_version}-forge-api:${jei_version}"

    // Development
    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    // Jetbrains Annotations
    compileOnly 'org.jetbrains:annotations:26.0.2'
    annotationProcessor 'org.jetbrains:annotations:26.0.2'
}

mixin {
    add sourceSets.main, 'cpaper.refmap.json'
    config 'cpaper.mixins.json'
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
    exclude ".cache"
}

jar {
    from('LICENSE') {
        rename { "${it}_${archivesBaseName}" }
    }
    manifest {
        attributes([
                "Specification-Title"     : "cpaper",
                "Specification-Vendor"    : "chemiofitor",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "chemiofitor",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

